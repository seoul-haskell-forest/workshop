# 하스켈 배움터 6일

## 준비물

* 컴퓨터에 PostgreSQL이 설치되어 있지 않다면 `brew`나 `Docker`로 설치
  ```sh
  $ brew install postgresql@16
  ...
  $ brew services start postgresql@16
  ```

  * 도커 설치 참고, https://diary-developer.tistory.com/20

* PostgreSQL에 `todoapp` 데이터베이스를 만들기
  ```
  create database todoapp
  ```

* `todoapp` 데이터베이스에 다음과 같이 `tasks` 테이블 만들기

  ```sql
  CREATE TABLE tasks (
    id VARCHAR(26) PRIMARY KEY,
    content VARCHAR(255) NOT NULL,
    completed BOOLEAN NOT NULL,
    craeted_at TIMESTAMPTZ NOT NULL,
    updated_at TIMESTAMPTZ
  );
  ```

* `package.yaml`을 열어 다음 의존성을 추가하고 `stack build`로 패키지를 컴파일
  
```yaml
dependencies:
  ...
  - stm
  - postgresql-simple
```

## 지난 시간에 만든 코드

* `Domain.Todo.Task`

```haskell
module Domain.Todo.Task where

import Data.Text (Text, length)
import Data.Time (UTCTime)
import Data.ULID (ULID)
import Prelude hiding (id, length)

data Task = Task
  { id :: ULID,
    content :: Text,
    completed :: Bool,
    createdAt :: UTCTime,
    updatedAt :: Maybe UTCTime
  }
  deriving (Show)

mkTask :: ULID -> Text -> UTCTime -> Maybe Task
mkTask taskId content createdAt =
  if length content > 12
    then
      Nothing
    else
      Just $
        Task
          { id = taskId,
            content = content,
            createdAt = createdAt,
            completed = False,
            updatedAt = Nothing
          }
```

* `Domain.Todo.UseCase`

```haskell
import Control.Error.Util ((??))
import Control.Monad.Except (runExceptT)
import Control.Monad.IO.Class (MonadIO, liftIO)
import Data.Text (Text)
import Data.Time (getCurrentTime)
import Data.ULID (getULID)
import Domain.Todo.Task (Task, mkTask)

createTask :: (MonadIO m) => Text -> m (Either Text Task)
createTask content = runExceptT $ do
  taskId <- liftIO getULID
  now <- liftIO getCurrentTime
  mkTask taskId content now ?? "Task content must be less than 12 characters"
```

* `Main.hs`

```haskell
module Main where

import qualified Domain.Todo.UseCase as TaskUseCase

main :: IO ()
main = do
  result <- TaskUseCase.createTask "Hello"
  print result
```

## 실습

* `Domain.Todo.TaskRepo` 모듈 만들기
* `save :: Task -> m Bool` 함수를 하나 갖는 `TaskRepo` 타입 클래스 정의하기
* `Domain.Todo.UseCase` 모듈의 `createTask` 함수에서 `save`를 불러 `Task`를 저장하기
* 저장에 실패하면 적절한 에러 메시지를 돌려주기

```haskell
createTask :: (MonadIO m, TaskRepo m) => Text -> m (Either Text Task)
createTask content = runExceptT $ do
  taskId <- liftIO getULID
  now <- liftIO getCurrentTime
  task <- mkTask taskId content now ?? "Task content must be less than 12 characters"
  result <- lift $ TaskRepo.save task
  if result
    then pure task
    else throwError "Task save failed"
```

## 실습

* `Main`에서 `createTask`를 사용할 수 있도록 `App` 타입을 만들고 임의의 `TaskRepo`, `save` 함수를 구현하기

```haskell
newtype App a
  = App {unApp :: IO a}
  deriving (Functor, Applicative, Monad, MonadIO)

main :: IO ()
main = do
  result <- unApp $ TaskUseCase.createTask "Hello"
  print result

instance TaskRepo App where
  save _ = pure True
```

## 하스켈에서 바꿀 수 있는 값: TVar (STM)

* `stm` 패키지에 `Control.Concurrent.STM.TVar`에 소프트웨어 트랜젝셔널 메모리가 구현되어 있다

```haskell
import Control.Concurrent.STM (atomically)
import Control.Concurrent.STM.TVar (newTVarIO, readTVarIO, writeTVar, modifyTVar, TVar)

main :: IO ()
main = do
  tvar <- newTVarIO (0 :: Int)
  x <- readTVarIO tvar
  print x
  atomically $ writeTVar tvar 1
  atomically $ modifyTVar tvar (\x -> x + 41)
  x <- readTVarIO tvar
  print x
```

## 실습 

* 할 일을 STM에 저장하기 위해 `[(ULID, Task)]` 타입을 `Database` 별칭으로 지정했다.
* 튜플의 앞에 있는 ULID는 `task.id` 값이다.

```haskell
type Database = TVar [(ULID, Task)]
```

* 할 일 데이터베이스에 할 일을 추가하는 `addTask :: Database -> Task -> IO ()`를 만들어라
  * 리스트 앞에 항목을 추가하기 함수 `(:)`
    * `1 : [2,3] == [1,2,3]`
  * 레코드 필드 디스트럭처링
    ```haskell
    addTask :: Database -> Task -> IO ()
    addTask db task@(Task {id = taskId}) = undefined
    ```

```haskell
instance TaskRepo App where
  save task@(Task {id = taskId}) = do
    db <- liftIO $ newTVarIO [(taskId, task)]
    liftIO $ atomically $ modifyTVar db (\tasks -> (taskId, task) : tasks)
    tasks <- liftIO $ readTVarIO db
    liftIO $ print tasks
    pure True
```

## 실습 

* `TaskRepo`, `save` 함수를 고쳐서 빈 리스트로 TVar Database를 만들고 `addTask`를 불러 할 일을 STM에 추가해보자

## 생각해보기

* `save` 함수는 부를 때 만다 빈 리스트로 새 Database를 만들기 때문에 문제가 있다.
* 어떻게 하면 Database를 유지할 수 있을까?

## 실습

* Reader 모나드에 Database를 담으면 Reader 모나드를 쓸 수 있는 곳에서 `ask`로 언제든 Database를 가져올 수 있다.
* `Reader Database a`를 쓸 수 있도록 `App` 타입을 고쳐보자

## 실습

* `Main`에서 STM에 Task가 추가되어 있는지 다음과 같이 출력해보자. 
  ```haskell
  main = do
    ...
    tasks <- readTVarIO db
    print tasks
  ```

## PostgreSQL Database 사용하기

* 하스켈에 다양한 PostgreSQL 패키지가 있지만 가장 간단하고 직관적인 `postgresql-simple` 패키지를 사용하자.

### 데이터베이스 연결과 해제

* `connect :: ConnectInfo -> IO Connection`와 `close :: Connection -> IO ()` 함수로 데이터베이스에 연결하면 
  `Database.PostgreSQL.Simple` 모듈의 `Connection` 값을 얻을 수 있다.
* `connect`에는 다음과 같이 `ConnectInfo`를 넘긴다. 
  ```haskell
  ConnectInfo
      { connectHost = "localhost",
        connectPort = 5432,
        connectDatabase = "todoapp",
        connectUser = "eunmin",
        connectPassword = ""
      }
  ```

### 실습

* `Reader` 모나드에 `Database.PostgreSQL.Simple` 모듈의 `Connection` 값을 넣도록 바꾸시오. `TaskRepo`의 
  `save` 함수의 `addTask`는 임시로 주석처리

### 타입 변환하기

* 기본적으로 쿼리의 인자로 튜플을 쓸 수 있다. (특별하게 값이 하나인 튜플을 위해 Only 값 생성자가 있다) 하지만 타입을 쓰는 것이
  더 좋다.
* `ToRow` 타입 클래스로 타입을 순서가 있는 `postgresql-simple`의 `Action` 타입으로 바꾼다.
  * https://hackage.haskell.org/package/postgresql-simple-0.7.0.0/docs/Database-PostgreSQL-Simple-ToRow.html#t:ToRow
* `ToField` 타입 클래스로 타입을 `postgresql-simple`의 `Action` 타입으로 바꾼다.
  * https://hackage.haskell.org/package/postgresql-simple-0.7.0.0/docs/Database-PostgreSQL-Simple-ToField.html#t:ToField
  
* `ToRow`는 데이터의 값 순서대로 자동으로 바꿔주는 기본 인스턴스 구현이 있다.

  ```haskell
  instance ToRow Task
  ```

* 자주 쓰는 하스켈 타입에 대해 `ToField` 구현이 있어서 대부분 `ToRow` 기본 구현을 쓸 수 있지만 커스텀 타입은 `ToFiled`
  인스턴스를 만들워줘야 한다. 

  ```haskell
  instance ToField ULID where
    toField ulid = toField (show ulid)
  ```

* `FromRow`는 `ToRow`와 반대로 데이터베이스 타입을 하스켈 타입으로 바꿀 때 쓴다. 마찬가지로 `FromField`도 있다. 역시
  순서를 기반으로 자동 구현을 해주지만 `FromField`는 직접 구현해야 한다. 다음은 `ULID`에 대한 `FromField` 
  단순한(안전하지 않은) 구현이다.

  ```haskell
  instance FromField ULID where
    fromField f mbs = read <$> fromField f mbs
  ```

### Orphan class instance 컴파일러 경고에 관해

* GHC는 기본적으로 데이터가 정의된 곳에서 인스턴스를 만들도록 하고 있다. 그렇지 않은 경우 Orphan class instance 경고를
  표시한다.
* 설계 상 인스턴스 구현을 다른 곳에 둬야 하는 경우가 있는데, 이런 경우 `package.yaml`에 컴파일러 옵션으로 경고를 무시 
  할 수 있다.
  
  ```yaml
  ghc-options:
  ...
  - -Wno-orphans
  ```

### 실습

* `Task`에 대해 `ToRow` 인스턴스를 만드시오. 어떤 필드에 `ToField` 인스턴스가 없다면 `ToField` 인스턴스도 만드시오


### 쿼리하기

* 조회용 `query` 함수와 변경용 `execute` 함수
* `execute :: ToRow q => Connection -> Query -> q -> IO Int64`
  ```haskell
  ...
  -- IO 모나드 에서
  affectedCount <- execute conn "INSERT INTO tasks (id, content, completed, craeted_at, updated_at) VALUES (?, ?, ?, ?, ?)" task
  ```
* `query :: (ToRow q, FromRow r) => Connection -> Query -> q -> IO [r]`
  ```haskell
   result <- query conn "SELECT * FROM tasks where id = ?" (Only ("1" :: String))
  ```

### 실습

* `TaskRepo`의 `save` 함수가 다음과 같은 쿼리로 데이터베이스에 저장할 수 있도록 고치시오.

  ```sql
  "INSERT INTO tasks (id, content, completed, craeted_at, updated_at) VALUES (?, ?, ?, ?, ?)"
  ```

## bracket 패턴

* 반납이 필요한 리소스를 사용하다가 예외(0/1 같은)가 발생하는 경우에도 안전하게 리소스 반납을 하기위해 `braket` 패턴을 
  사용한다.

  ```haskell
  import Control.Exception (bracket)

  bracket :: IO a     -- 리소스 얻기
          -> (a -> IO b) -- 리소스 해제
          -> (a -> IO c) -- 리소스 사용
          -> IO c
  ```

### 실습

* `Main`에서 데이터베이스 연결을 얻고 닫는 코드를 `bracket`을 사용해 리팩터링 하라
