# 하스켈 배움터 1일

## 코드 구조

```haskell
module Main (main) where

main :: IO ()
main = do
  putStrLn "hello world"
```

* 파일명과 모듈 이름
* `이름 = 값`, `이름 :: 타입`, 순서는 관계 없음
* 들여쓰기
* 타입 추론이 되기 때문에 타입 지정은 필수가 아님, 하지만 최상위 타입은 관행으로 지정
* 주석
  ```haskell
  -- 한 줄 주석
  {- 
  여러
  줄
  주석
  -}
  ```

## 기본 타입

### 숫자
  
```haskell
value :: Integer
value = 1

value :: Int
value = 1

value :: Double
value = 1.0

value :: Float
value = 1.0
```

### 문자
```haskell
value :: Char
value = '가'
```

### 문자열
```haskell
value :: String
value = "가나다"
```

### 리스트
```haskell
value :: [Char]
value = ['가', '나', '다']
```
* `String`과 `[Char]`은 같다

### 튜플
```haskell
value :: (String, Int)
value = ("가나다", 42)
```

### 불리언
```haskell
value :: Bool
value = True
```

### 함수
```haskell
add :: (Int -> Int) -> Int
add x y = x + y

result = add 2 3
```

### 함수 부분 적용
```haskell
add2 :: Int -> Int
add2 = add 2

result = add2 3

add :: Int -> (Int -> Int)
add x y = x + y

add :: Int -> Int -> Int
add x y = x + y
```

### 이름 없는 함수에 이름 붙이기
```haskell
add :: Int -> Int -> Int
add = \x y -> x + y

add :: Int -> (Int -> Int)
add = \x -> (\y -> x + y)

add :: Int -> Int -> Int
add = \x -> \y -> x + y

add :: Int -> Int -> Int
add x = \y -> x + y
```

### 중위 함수로 부르기
```haskell
result = 1 `add` 2
```

### 중위 함수 만들기
```haskell
(+++) :: Int -> Int -> Int
(+++) x y = x + y

result = 1 +++ 2
```
중위 함수는 기호를 괄호로 감싸서 정의, 기호가 아닌 것은 안 됨

중위 함수를 전위 함수로 부르기
```haskell
result = (+++) 1 2
```

`+`도 중위 함수이기 때문에 다음과 같이 구현할 수 있음

```haskell
(+++) :: Int -> Int -> Int
(+++) = (+)
```

### 섹션

중위 함수의 부분 적용 구문

```haskell
(+1) 2
-- 3
(1+) 2
-- 3
(/2) 10
-- 5.0
(2/) 10
-- 0.2
```

## 타입 만들기

```haskell
data Point = Point Double Double deriving (Show, Eq)

value :: Point
value = Point 1.0 1.0
```

* `data 타입이름 = 타입생성자이름 타입 타입 ...` 형식
* 타입생성자로 값을 만들 수 있음 
* `Point :: Double -> Double -> Point` 생성자 함수가 생김
* `deriving (Show, Eq)`에 대해서는 다음 시간에 다룰 예정, 출력과 동등 비교를 위해 일단 붙이기

### 생성자 함수가 여러 개인 타입

```haskell
data Shape
  = Rect Double Double
  | Circle Double
  deriving (Show, Eq)
  
value1 :: Shape
value1 = Rect 1.0 1.0

value2 :: Shape
value2 = Circle 2.0
```

### 생성자 함수에 인자가 없는 타입

```haskell
data Bool = True | False deriving (Show, Eq)
```

### 함수의 패턴 매칭과 디스트럭처링

함수 값에 따라 구현을 바꿈

```haskell
crazyAdd :: Int -> Int -> Int
crazyAdd 0 0 = 42
crazyAdd 1 x = x
crazyAdd x y = x + y
```

`Shape` 타입의 면적 구하는 함수

```haskell
area :: Shape -> Double
area (Rect w h) = w * h
area (Circle r) = pi * r * r

result1 = area (Rect 1.0 1.0)
result2 = area (Circle 2.0)
```

생성자 함수 이름으로 구현을 고르고 생성자 함수의 인자를 함수의 인자로 디스트럭처링해서 사용

### $ 구문
```haskell
result1 = area $ Rect 1.0 1.0
result2 = area $ Circle 2.0

result3 = area (Rect 1.0 (area (Circle 2.0)))
result3 = area $ Rect 1.0 (area (Circle 2.0))
result3 = area $ Rect 1.0 $ area (Circle 2.0)
result3 = area $ Rect 1.0 $ area $ Circle 2.0
```

### 레코드

생성자 함수에 이름(필드명)을 붙여 타입을 정의

```haskell
data Shape
  = Rect {width :: Double, height :: Double}
  | Circle {radius :: Double}
  deriving (Show, Eq)

rect = Rect 1.0 1.0

rect = Rect {height = 1.0, width = 1.0}
```

레코드 패턴 매칭

```haskell
area :: Shape -> Double
area (Rect w h) = w * h
area (Circle r) = pi * r * r

area :: Shape -> Double
area Rect {width = w, height = h} = w * h
area Circle {radius = r} = pi * r * r

area :: Shape -> Double
area (Rect _ 0) = 0
area (Rect 0 _) = 0
area (Rect w h) = w * h
area (Circle 0) = 0
area (Circle r) = pi * r * r

area :: Shape -> Double
area Rect {height = 0} = 0
area Rect {width = 0} = 0
area Rect {width = w, height = h} = w * h
area Circle {radius = 0} = 0
area Circle {radius = r} = pi * r * r
```

필드명을 셀렉터 함수로 사용

```haskell
area :: Shape -> Double
area rect@Rect {} = width rect * height rect
area circle@Circle {} = pi * radius circle * radius circle
```

필드명은 겹칠 가능성이 많아서 Prefix를 붙여서 사용 (다른 방법으로 더 쉽게 사용하는 법은 뒤에서 다룸)

```haskell
data Shape
  = Rect {rectWidth :: Double, rectHeight :: Double}
  | Circle {circleRadius :: Double}
  deriving (Show, Eq)
```

### newtype

생성자 함수가 하나고 인자가 하나면 `data` 대신 `newtype` 키워드로 정의, 특별하게 사용할 수 있음

```haskell
newtype Value = Value {unValue :: Int} deriving (Show, Eq)
```

## 제어 구문

들여쓰기는 중요

```haskell
foo :: Int -> String
foo x = 
  if x > 10
    then "bar"
    else "baz"
```

### Bool 연산자

비교를 위한 `>`, `<`, `==`, `>=`, `<=`, `/=`(다름), 조건 조합을 위한 `&&`, `||`

### `else if`

```haskell
foo :: Int -> String
foo x = 
  if x > 10 then "bar"
    else if x > 5 then "five"
    else "baz"
```


### 가드

```haskell
foo :: Int -> String
foo x
  | x > 10 = "bar"
  | x > 5 = "five"
  | otherwise = "baz"
```

### case 구문과 디스트럭처링

```haskell
area :: Shape -> Double
area value =
  case value of
    Rect w h -> w * h
    Circle r -> pi * r * r
```

## 로컬 바인딩

함수 내부에서만 값에 이름을 붙여서 씀, `where`와 `let-in` 구문이 있음

### `where` 구문

```haskell
doubleArea :: Double -> Double
doubleArea r =
  area * 2
  where
    area = pi * r * r
    pi = 3.14
```

### `let-in` 구문

```haskell
area :: Double -> Double
area r = 
  let pi = 3.14
  in  pi * r * r
```

## 모듈

### 모듈 사용하기

```haskell
import Data.List

numUniques :: (Eq a) => [a] -> Int
numUniques = length . nub
```

### 모듈의 특정 값(함수 포함)만 사용하기

```haskell
import Data.List (length, nub)
```

### 특정 값만 빼고 모두 가져오기

```haskell
import Data.List hiding (nub)
```

### 이름 충돌 막기

모듈 전체에 이름을 붙여 쓰는 방법과 모듈 이름을 다른 이름으로 바꿔서 붙여 쓰는 방법

```haskell
import qualified Data.Map

Data.Map.filter
```

```haskell
import qualified Data.Map as M

M.filter
```

### 모듈 만들고 내보내기

* 모듈 이름은 대문자로 시작, 경로와 파일명을 맞춰야 함 (예, `Data.Map` 모듈은 `src/Data/Map.hs` 파일)

```haskell
module Data.Map where

-- 코드
```

* 공개 값과 비공개 값 모듈 이름 뒤에 괄호로 내보낼 값을 지정하면 나머지는 비공개

```haskell
module Lib
    ( someFunc
    ) where

message :: String
message = "someFunc"

someFunc :: IO ()
someFunc = putStrLn message
```

### 생성자 함수를 내보내기

```haskell
module Lib (Shape (Circle, Rectangle)) where

data Shape = Circle Point Float | Rectangle Point Point
```

모든 생성자 함수 내보내기

```haskell
module Lib (Shape (..)) where

data Shape = Circle Point Float | Rectangle Point Point
```

### 생성자 함수 가져오기

```haskell
import Lib (Shape (Circle, Rectangle))
```

### 모든 생성자 함수 가져오기

```haskell
import Lib (Shape (..))

f :: Point -> Shape 
f x = Circle x 1.0
```

* 타입만 내보낼 때는 괄호를 쓰지 않아도 됨

## 리스트

### 리스트 패턴 매칭과 디스트럭처링

```haskell
length :: [a] -> Int
length []     = 0
length (x:xs) = 1 + length xs
```
### `..`

```haskell
[1, 2, 3, 4] == [1 .. 4]

head [1 ..]
-- 1
```

### 주요 리스트 함수

```haskell
head [1 .. 4]
-- 1

last [1 .. 4]
-- 4

tail [1 .. 4]
-- [2,3,4]

init [1 .. 4]
-- [1,2,3]

take 2 [1 .. 4]
-- [1,2]

drop 2 [1 .. 4]
-- [3,4]

null []
-- True

1 `elem` [1 .. 4]
-- True

[1 .. 4] !! 3
-- 4

concat [[1, 2], [], [3, 4]]
-- [1,2,3,4]

map (+1) [1 .. 4]
-- [2,3,4,5]


filter (>2) [1 .. 4]
-- [3,4]

foldr (+) 0 [1 .. 4]
- 10
```

### 리스트 통합(comprehension)

```haskell
[ x * 2 | x < [1 .. 4]]
-- [2,4,6,8]
```

## 실습 문제

* 개발자, 디자이너, 매니저 생성자 함수를 갖는 Role 타입 만들기
* name(문자열), role(Role), salary(숫자) 타입을 갖는 Person 레코드 만들기
* `sumSalary :: [Person] -> Int` 함수 만들기 
* 개발자인 경우 salary 1,000 보너스, 디자이너인 경우 500 보너스를 붙여서 `sumSalary`를 계산하도록 하기
  * 보너스를 계산하는 함수를 패턴 매칭으로 따로 만들어 보기
* Person을 모듈로 만들고 sumSalary와 Person, Role 등을 내보내서 Main에서 예제와 함게 sumSalary를 불러보기