# 하스켈 배움터 2일

## 프로젝트 만들기 (기본 템플릿인 hpack 버전)

```sh
$ stack --resolver lts-21.25 new hello
```

## REPL

`-- >>>` 으로 코드에서 값을 평가할 수도 있지만 다음과 같이 따로 REPL을 실행할 수 있다

```sh
$ stack repl
```

## 타입 변수

어떤 값을 인자로 받아 그대로 돌려주는 함수의 타입은?

```haskell
identity :: Int -> Int
identity x = x

identity :: String -> String
identity x = x
```

```haskell
identity :: a -> a
identity x = x
```

`a`은 타입 변수로 어떤 타입을 말하는 것이다. `a -> a`는 입력 타입과 출력 타입이 같다는 것을 말한다. 

```haskell
identity :: any -> any
identity x = x
```

`a` 대신 소문자로 시작하는 아무 글자를 써도 된다.

인자 두 개를 받아서 두 번째 값을 돌려주는 함수

```haskell
second :: a -> b -> b
second x y = y
```

`a`, `b`를 썼기 때문에 두 인자 타입은 달라도 된다. 하지만 결과는 두번째 인자 타입과 같아야 한다

## 타입 클래스

더하기 함수 `(+)`는 어떻게 `Int`와 `Double`을 함께 처리할까?

```haskell
int :: Int
int = 1 + 1

double :: Double
double = 2.0 + 3.0
```

`(+) :: Int -> Int -> Int`와 `(+) :: Double -> Double -> Double` 둘 다 처리할 수 있는 다형적 함수이다.

`(+)`의 실제 타입에 대해 알아보기, REPL에서 `:t` 뒤에 값을 쓰면 타입이 나온다. 

```haskell
-- >>> :t (+)
-- (+) :: Num a => a -> a -> a
```

어떤 타입 `a` 인자 두 개를 받아 같은 타입의 `a`를 돌려준다. 하지만 `a` 타입은 `Num` 이라는 제약이 있다. 여기서 `Num`을 
타입 클래스라고 한다. 

`Int`나 `Double`은 `Num` 타입 클래스 제약을 따르고 있기 때문에 쓸 수 있다. 제약을 따른 다는 것은 타입 클래스의 인스턴스가 
있다고 말한다.

`:info` 뒤에 타입을 쓰면 타입에 대한 자세한 내용이 나온다

```haskell
-- >>> :info Int
-- type Int :: *
-- data Int = I# Int#
--   	-- Defined in ‘GHC.Types’
-- instance Bounded Int -- Defined in ‘GHC.Enum’
-- instance Enum Int -- Defined in ‘GHC.Enum’
-- instance Integral Int -- Defined in ‘GHC.Real’
-- instance Num Int -- Defined in ‘GHC.Num’
-- instance Ord Int -- Defined in ‘GHC.Classes’
-- instance Read Int -- Defined in ‘GHC.Read’
-- instance Real Int -- Defined in ‘GHC.Real’
-- instance Eq Int -- Defined in ‘GHC.Classes’
-- instance Show Int -- Defined in ‘GHC.Show’
```

`Int`는 `Enum`, `Show`등 여러 타입 클래스의 인스턴스가 있는 것을 볼 수 있다. 타입 클래스는 타입 클래스가 선언된 곳이나 
타입이 선언된 곳에서 인스턴스를 만들지 않아도 되기 때문에 확장성이 좋다. 사용하는 곳에서 인스턴스가 구현된 모듈을 import 하면
된다. (* 인스턴스를 따로 export 할 필요는 없다)

### 실습 1

`Int`와 `Double`등 `Num` 타입 클래스의 인스턴스가 있다면 `1`을 증가시키는 `inc` 함수를 정의하고 구현하기, 힌트 타입 
제약은 `(+)` 함수의 시그니처를 참고!

### 타입 클래스 인스턴스 만들기

어떤 타입 클래스 인스턴스는 `instance` 구문으로 만들 수 있다.

```haskell
instance 타입클래스이름 타입이름 where
  함수 = 구현
  함수 = 구현
  ...
```

예를 들어 `Num` 타입 클래스는 `(+)` 함수를 구현해야 한다. `MyValue`라는 나만의 타입을 만들고 `(+)`를 쓰기 위해 다음과
같이 인스턴스를 만들 수 있다. `Num`이 요구하는 함수를 다 구현해야 하지만 구현하지 않아도 컴파일러 경고가 나지만 컴파일은 되고 
만약 사용한다면 런타임 에러가 난다.

```haskell
value1 :: MyValue
value1 = MyValue 1

value2 :: MyValue
value2 = MyValue 2

instance Num MyValue where
  (+) (MyValue x) (MyValue y) = MyValue (x + y)
  -- 나머지는 구현하지 않아서 컴파일러가 경고를 준다

value :: MyValue
value = value1 + value2
```

### 실습 2

`x`와 `y` 좌표를 갖는 `Point` 레코드 타입을 만들고 두 `Point`에 `(+)`를 사용할 수 있게 `Num` 인스턴스를 만들기
더하는 방법은 `x` 값과 `y` 값을 각각 더하기

### 타입 클래스 선언하기

`class` 구문으로 타입 클래스를 정의 할 수 있다.

```haskell
class 타입클래스이름 타입변수 where
  값 :: 타입
  ...
```

### 실습 3

다음과 같은 `YesNo` 타입 클래스를 정의하고 `Int`, `Bool`, `리스트` 타입에 대해 각각 인스턴스를 만들기

```haskell
class YesNo a where
    yesno :: a -> Bool
```

`yesno`는 `Int`인 경우 `0`일 때만 `False`, `Bool`인 경우 `False`면 `False`, `리스트`은 `[]`면 `True` 
나머지는 `False`

### 타입 클래스의 기본 구현 

타입 클래스는 기본 구현을 갖을 수 있다. 기본 구현이 있다면 인스턴스를 만들 때 구현해주지 않아도 된다.

```haskell
class YesNo a where
    yesno :: a -> Bool
    yesno _ = False
```

위와 같이 `yesno`의 기본 구현을 무조건 `False`로 해주면 `YesNo` 인스턴스를 만들 때 `yesno` 함수를 구현하지 않아도 된다.

## Deriving

내가 만든 타입을 출력하려면 출력하는 방법을 모르기 때문에 에러가 난다

```haskell
data Point = Point Float Float

-- >>> Point 1.0 2.0
-- No instance for (Show Point) arising from a use of ‘evalPrint’

-- >>> print $ Point 1.0 2.0
-- No instance for (Show Point) arising from a use of `print'
```

모두 `Show` 타입 클래스의 인스턴스가 없기 때문이다. `Show` 타입 클래스에는 `show` 함수가 있는데 `a -> String` 어떤 
값을 문자열로 바꿔주는 함수이다.

```haskell
class Show a where
  show :: a -> String
  ...
```

## 실습 4

위 `Point` 타입의 `Show` 타입 클래스를 만들어 `main` 함수에서 `print` 함수로 출력해보세요. 문자열은 `(++)` 함수로 합칠
수 있다.

### deriving

타입을 정의할 때 마지막에 `deriving (타입클래스)`를 써주면 자동으로 인스턴스가 생긴다. 자동으로 만들 수 있는 이유는 타입 
클래스의 기본 구현을 사용한다. 

```haskell
data Point = Point Double Double deriving (Show)

-- >>> Point 1.0 1.0
-- Point 1.0 1.0
```

기본적으로 하스켈에 정의된 타입 클래스에 대해 deriving 할 수 있고 `DeriveAnyClass` 언어 확장을 사용하면 사용자가 만든 
타입 클래스도 deriving 할 수 있다.

```haskell
{-# LANGUAGE DeriveAnyClass #-}

module Main where

class YesNo a where
  yesno :: a -> Bool
  yesno _ = False

data Point = Point Double Double deriving (Show, YesNo)
```

### 언어 확장

모듈의 가장 위에 `{-# LANGUAGE 언어확장 #-}`으로 GHC 언어 확장을 활성화 수 있다. 하스켈 컴파일러마다 지원하는 언어 확장이
다르기 때문에 언어 확장은 하스켈 표준이라고 할 수 없지만 대부분 GHC 컴파일러를 쓰기 때문에 언어 확장을 많이 사용한다. 항상
언어 확장을 활성화 하려면 `package.yaml` 파일을 쓰는 경우 `default-extensions`키에 정의할 수 있다. 자주 사용하는 언어 
확장은 다음과 같다. 설명은 필요할 때 하도록 하겠다.

```text
ConstraintKinds
DeriveGeneric
DerivingStrategies
GeneralizedNewtypeDeriving
InstanceSigs
KindSignatures
LambdaCase
OverloadedStrings
RecordWildCards
ScopedTypeVariables
StandaloneDeriving
TupleSections
TypeApplications
ViewPatterns
```

## 펑터와 타입 생성자

### 실습 5

`Null`이거나 `Int` 값을 같는 타입 `OptionalInt` 타입을 정의하기

### 실습 6

`OptionalInt` 두 개를 더 할 수 있게 `Num` 인스턴스를 만들고 `(+)` 함수로 더하기, 하나라도 `Null`이면 결과는 `Null`

### OptionalInt의 일반화

```haskell
data Optional a = Value a 
                | Null 
                deriving (Show)
```

`Optional`은 타입이 아니고 타입 변수(`a`) 한 개를 받는 타입 생성자이다. `Optional Int`는 타입이다.
(타입 레벨 관점에서 Kind는 `* -> *` 또는 `Type -> Type`, 뒤에서 다룰 예정)

### 실습 7

일반화 된 `Optional`의 `(+)` 인스턴스 만들기

### Optional 사용하기

### 실습 8

* 아래 `inc` 함수를 사용해서 `Optional a` 타입에 쓸 수 있는 `incOptional`를 구현하시오. `a`는 `Num` 인스턴스가 필요
  `Null` 값일 때는 결과가 `Null`

  ```haskell
  inc :: (Num a) => a -> a
  inc = (1 +)
  ```

* 아래 `hello` 함수를 사용해서 `Optional String` 타입에 `helloOptionalString` 함수를 정의하고 구현하시오.

  ```haskell
  hello :: String -> String
  hello = ("Hello " ++)
  ```

### 공통 기능의 일반화

#### 실습 9

`incOptional`와 `helloOptional`는 Optional로 감싸있는 값에 Null이 아닐 때만 일반 함수를 적용하는 공통 코드가 있다.
이 코드를 일반화 한 `mapOptional` 함수를 구현해보자.

```haskell
mapOptional :: (a -> b) -> Optional a -> Optional b
```

`mapOptional`과 `inc`, `hello`를 적용해 `incOptional`, `helloOptionalString`를 사용해보라