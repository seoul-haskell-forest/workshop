# 하스켈 배움터 3일

## 펑터

### 복습 

```haskell
data Optional a
  = Value a
  | Null
  deriving (Show)

mapOptional :: (a -> b) -> Optional a -> Optional b
mapOptional f (Value x) = Value (f x)
mapOptional _ Null = Null
```

### 타입 생성자와 리스트

* `[]`도 타입 생성자
* `data List a = ...` 대신 `data [] a = ...`로 쓴다.
* `[a]` 형태로 쓸 수 있다.

### 실습 10

* `mapList` 구현하기
* 리스트 안에 있는 값에 `f` 적용하기
* 효과는 모든 항목에 적용
* 힌트: 리스트 map 함수?

```haskell
mapList :: (a -> b) -> [a] -> [b]
mapList f xs = map f xs
```

### mapXXX의 일반화

* `mapOptional :: (a -> b) -> Optional a -> Optional b`
* `mapList :: (a -> b) -> [a] -> [b]`
* 일반화 하면 `Optional`, `[]` 대신 타입 생성자를 타입 변수로 바꾼다.

```haskell
mapAll :: (a -> b) -> f a -> f b
```

* `maAll`을 타입 클래스로 만들면?
* 의미
  * 타입 생성자로 감싼 어떤 값에 `f`를 적용
  * 효과 발생

```haskell
class Mapable f where
	mapAll :: (a -> b) -> f a -> f b 
```

### 실습 11

* `Optional` 타입 생성자의 `Mapable` 인스턴스를 만들기, 구현은 `mapOptional`과 같다.
* `[]` 타입 생성자의 `Mapable` 인스턴스 만들기, 구현은 `mapList`와 같다.

```haskell
instance Mapable [] where
  mapAll f xs = map f xs

instance Mapable Optional where
  mapAll f (Value x) = Value (f x)
  mapAll _ Null = Null
```

### 생각해보기

`Int` 타입도 `Mapable` 인스턴스를 만들 수 있을까?

만들 수 없다.

### 펑터의 의미와 Maybe 타입

* 하스켈 `Functor` 타입 클래스

```haskell
class Functor f where
	fmap :: (a -> b) -> f a -> f b 
```

* `Optional`은 하스켈에서 `Maybe`로 정의 되어 있음 (`Functor` 인스턴스 있음)

```haskell
data Maybe a = Just a | Nothing
```

```haskell
-- >>> fmap inc (Just 1)
-- Just 2

-- >>> fmap inc Nothing
-- Nothing

-- >>> fmap (prefix "Hello") $ Just "World"
-- Just "Hello World"

-- >>> fmap inc [1, 2, 3]
-- [2,3,4]
```

* `Functor`는 하스켈에서 중위 함수인 `<$>`로 많이 쓴다.

```haskell
-- >>> inc <$> (Just 1)
-- Just 2

-- >>> inc <$> [1, 2, 3]
-- [2,3,4]
```

### Either 타입

* `Maybe`는 결과에 값이 없을 때
* `Either`는 결과에 에러가 있을 때 내용과 함께 

```haskell
data Either a b = Left a | Right b
```

```haskell
failValue :: Either String Int
failValue = Left "Invalid Number"

successValue :: Either String Int
successValue = Right 1
```

* `Either`도 `Functor` 인스턴스 
  * 효과는 `Left` 값이 아닐 때만 `f`를 적용

```haskell
-- >>> inc <$> successValue
-- Right 2

-- >>> inc <$> failValue
-- Left "Invalid Number"
```

### 생각해보기

* `Either a b`는 타입 생성자가 두 개인데 `Functor` 인스턴스는 어떻게 만들까?

```haskell
class Functor f where
	fmap :: (a -> b) -> f a -> f b 
```

타입 생성자의 부분 적용으로 만들 수 있다.

```haskell
instance Functor (Either a) where
	fmap = undefined
```

## 모나드

* 튜플의 리스트를 해시 맵처럼 쓰기

```haskell
ids :: [(String, Int)]
ids = [("Todd", 32), ("Eunmin", 424)]

-- >>> lookup "Todd" ids
-- Just 32

-- >>> lookup "Hello" ids
-- Nothing
```

* `lookup`의 결과 타입은 `Maybe 값타입`, 값이 없으면 `Nothing`

### 실습 12

* `getAgeByPhone` 구현하기

```haskell
phones :: [(String, String)]
phones = [("123-123-123", "Todd"), ("000-000-000", "Eunmin")]

ids :: [(String, Int)]
ids = [("Todd", 32), ("Eunmin", 424)]

ages :: [(Int, Int)]
ages = [(32, 100), (42, 50)]

getAgeByPhone :: String -> Maybe Int
getAgeByPhone phone = undefined
```

```haskell
getAgeByPhone :: String -> Maybe Int
getAgeByPhone phone =
  case lookup phone phones of
    Just name ->
      case lookup name ids of
        Just id' -> lookup id' ages
        Nothing -> Nothing
    Nothing -> Nothing
```

### 펑터의 한계

* `getAgeByPhone`에 펑터를 사용할 수 있을까?

```haskell
getAgeByPhone :: String -> Maybe (Maybe (Maybe Int))
getAgeByPhone phone =
  ( \name ->
      ( \id ->
          lookup id ages
      )
        <$> (lookup name ids)
  )
    <$> lookup phone phones
```
* 펑터는 함수를 적용하고 `f` 생성자로 감싼다.
* `(<$>) :: (a -> b) -> f a -> a b`
* `b` 타입이 `f b`고 감싸지 않게 하면?

```haskell
class Monad m where
  bind :: (a -> m b) -> m a -> m b
```

* 하스켈에서는 중위 함수로 구현 `(>>=)`

```haskell
class Monad m where
  (>>=) :: m a -> (a -> m b) -> m b
```

* 연속 적용의 편의를 위한 순서
* `Maybe`는 `Monad` 인스턴스가 이미 있음
* `(>>=)` 연산자 우선 순위는 가장 낮음 (나중에 계산)

```haskell
getAgeByPhone :: String -> Maybe Int
getAgeByPhone phone =
  lookup phone phones >>= \name ->
    lookup name ids >>= \id ->
      lookup id ages
```

* Tip! 인자 순서를 바꿔주는 `flip`

```haskell
getAgeByPhone :: String -> Maybe Int
getAgeByPhone phone =
  lookup phone phones >>= flip lookup ids >>= flip lookup ages
```

### 모나드의 좋은 점

* 펑터 대신 모나드

```haskell
-- >>> fmap (\x -> inc x) $ Just 1
-- Just 2

-- >>> Just 1 >>= (\x -> Just (inc x))
-- Just 2
```

* 일반 값을 `m` 생성자로 감싸주는 `retrun` 함수

```haskell
class Monad m where
  (>>=) :: m a -> (a -> m b) -> m b
  return :: a -> m a
```

* 주의, 자바같은 언어의 `return`과 이름만 같다

```haskell
-- >>> Just 1 >>= (\x -> Just (inc x))
-- Just 2

-- >>> Just 1 >>= (\x -> return (inc x))
-- Just 2
```

### 실습 13

다음 값들을 이용해서 모나드를 활용해 `id` 값에 `inc`를 적용해 나이를 가져오는 `getAge'` 함수를 만들어라

```haskell
inc :: (Num a) -> a -> a
inc x = x + 1

ids :: [(String, Int)]
ids = [("Todd", 32), ("Eunmin", 424)]

ages :: [(Int, Int)]
ages = [(33, 100), (423, 50)]
```

```haskell
getAge :: String -> Maybe Int
getAge name = do
  id' <- lookup name ids
  let id'' = inc id'
  lookup id'' ages
```

### 실습 14

* `[]`도 모나드
* 모든 항목에 `inc`를 적용하고 다음에 모든 항목에 `show`를 적용하는 `foo` 함수를 구현하라.

```haskell
foo :: [Int] -> [String]
foo = undefined
```

```haskell
foo :: [Int] -> [String]
foo xs = do
  x <- xs
  let y = inc x
  return $ show y
```

### 모나드의 의미

* 어떤 값이 모나드로 감싸 있다면 그 값에 일반 함수를 적용할 수 있다. (펑터도 됨)
* 효과가 발생한다 (펑터도 됨)
* 연속해서 적용할 수 있다. 맥락

### 모나드 편의 문법

* `do` 편의 구문

```haskell
getAgeByPhone :: String -> Maybe Int
getAgeByPhone phone = do
  name <- lookup phone phones
  id <- lookup name ids
  age <- lookup id ages
  return age
```

* `do` 안에서 로컬 바인딩 `let` 쓰기

```haskell
getAge :: String -> Maybe Int
getAge name = do
  name <- lookup phone phones
  id <- lookup name ids
  let id' = inc id
  age <- lookup id' ages
  return age
```

## 자주 쓰는 모나드

모나드는 맥락 속에서 연속된 계산을 한다. `Maybe` 모나드인 경우 계산 중 결과가 `Nothing` 값이 나오면 다음 계산으로 이어지지
않는 맥락을 갖는다. 자주 쓰는 다른 모나드를 보자.

### Reader

모나드 계산 중에 언제나 외부 값을 불러서 쓸 수 있는 효과가 있는 모나드 만들기

#### 실습 18

어떤 타입 `r`을 인자로 받고 어떤 다른 타입 `a`를 돌려주는 함수 값 `r -> a`를 갖는 `Config r a` 타입을 레코드 타입으로 
정의하라.

```haskell
data Config r a = Config {x :: r -> a}
```

#### 실습 19
실습 18에서 만든 `Config r a`에 효과를 만들 것이다. `r`은 외부 값, `a`가 진짜 값이라고 할때, `42`라는 진짜 값을
갖는 `Config r a` 값을 만들고 진짜 값을 꺼내보자. `r`은 아무 값이나 넣어라.

```haskell
getValue :: Config String Int -> Int
getValue (Config f) = f ""
```
