# 하스켈 배움터 8일

## 준비물

* `package.yaml`에 `tests:` 아래 의존성에 아래와 같이 추가하고 `stack test` 실행하기

```yaml
tests:
  ...
  dependencies:
  ...
  - hspec
  - QuickCheck
  - quickcheck-instances
```

## 지난 시간 코드 정리 

- [todoapp08](./todoapp08)

## 테스트 코드 만들기

* https://hspec.github.io/
* HLS의 멀티 프로젝트 지원 어려움

### hspec 설정

* `./test/Spec.hs`를 다음과 같이 바꾸기, `./test` 아래 `Spec.hs`로 끝나는 파일을 찾아 테스트 코드로 인식해 준다.
  ```haskell
  {-# OPTIONS_GHC -F -pgmF hspec-discover #-}
  ```

### 코드 작성

* `./test/HelloSepc.hs` 간단한 테스트 만들고 실행해보기

```haskell
module HelloSpec where

import Relude
import Test.Hspec

spec :: Spec -- hspec-discover가 최상의 Spec 타입을 자동으로 찾는다
spec = do
  describe "(+) 함수" $ do -- 안 써도 되지만 결과를 볼 때 테스트가 많으면 도움이 됨
    it "1 더하기 1은 2" $ do 
      (1 + 1) `shouldBe` (3 :: Int) -- Expectation, 여러 개를 쓸 수 있다.
```

* `it`의 두 번째 인자 모나드 컨택스트인 `SpecM` 모나드 스택은 가장 아래 `IO`를 갖고 있기 때문에 `IO`를 쓸 수 있다.

```haskell
it "1 더하기 1은 2" $ do
  liftIO $ putStrLn "IO!" -- 모나드에서 IO를 쓸 수 있다
  (1 + 1) `shouldBe` (3 :: Int)
```

* 몇 가지 Expectation 함수 (https://hspec.github.io/expectations.html)
  * `shouldBe`, `shouldReturn`, `shouldSatisfy`, `shouldThrow`, `expectationFailure`
  * https://hackage-content.haskell.org/package/hspec-expectations-0.8.4/docs/Test-Hspec-Expectations.html

### 실행

* `stack test`로 실행  

```sh
Hello
  (+) 함수
IO!
    1 더하기 1은 2 [✘]

Failures:

  test/HelloSpec.hs:10:15: 
  1) Hello, (+) 함수, 1 더하기 1은 2
       expected: 3
        but got: 2

  To rerun use: --match "/Hello/(+) 함수/1 더하기 1은 2/" --seed 645911848

Randomized with seed 645911848

Finished in 0.0014 seconds
1 example, 1 failure
```

### 실습

* 몇 가지 예제로 `Domain.Todo.Task` 모듈의 `mkTask`를 성공하는 테스트와 실패(content가 12자 이상)하는 테스트인 
  `Domain.Todo.TaskSpec` 모듈을 작성 
  * `shouldSatisfy`와 `isJust`로 결과가 `Just`인지 확인하기
* 결과에 있는 updatedAt이 Nohting인지 확인하기

## 속성 기반 테스트

* 부수 효과가 없는 함수의 특별한 속성을 가지고 테스트한다.
* 예로 리스트 `reverse` 함수는 두 번 적용하면 원래 대로 돌아오는 속성이 있다.

  ```haskell
  it "reverse를 두 번하면 원본과 같아야 한다" $ do
      let sample = [1, 2, 3]
      (reverse . reverse $ sample) `shouldBe` sample
  ```
* QuickCheck 패키지 (대소문자 주의)
* 보통 속성 기반 테스트 라이브러리는 테스트 샘플을 여러 개 자동으로 만들어준다. (generative testing이라고도 한다)
* hspec 패키지는 quickcheck와 잘 통합되어 있다. `prop` 함수로 다음과 같이 테스트 할 수 있다.

  ```haskell
  prop "should be original when reverse of reverse" $
      \(sample :: [Int]) -> -- 타입을 기반으로 샘플 자동 생성, 다양한 샘플로 기본 100회 테스트 수행
        (reverse . reverse $ sample) `shouldBe` sample
  ```

### 속성 패턴

* 속성 패턴 (https://fsharpforfunandprofit.com/posts/property-based-testing-2/?ref=codebldr)
  * 경로는 다르지만 같은 목적지 (Different paths, same destination)
    ```haskell
    add 2 (add 1) == add 1 (add 2)
    ```
  * 다시 되돌리기 (There and back again)
    * `addition/subtraction`, `write/read`, `setProperty/getProperty`
    * `insert/contains`, `create/exists`
  * 변하지 않는 것 (Some things never change)
    * `sort`를 해도 항목 구성은 바뀌지 않는다.
  * 여러 번 해도 그대로 (The more things chagne, the more they stay the same)
    * 리스트에 distinct는 여러 번 불러도 값이 그대로다.
      ```haskell
      prop "여러 번 해도 한 번 한 것과 같다" $
        \(sample :: [Int]) ->
          (uniq . uniq . uniq $ sample) `shouldBe` uniq sample
      ``` 
  * 구조의 크기와 관계 없이 적용 (Solve a smaller problem first)
    * 리스트를 첫 번째 항목과 나머지 항목들로 쪼개는 함수는 더 작은 구조에도 쓸 수 있다.
    * 재귀적 구조의 속성
  * 복잡한 구현이지만 쉬운 검증 (Hard to prove, easy to verify)
    * 어떤 규칙이 있는 스트링 토크나이저는 구현하기 복잡하지만 확인하려면 그냥 문자열을 붙이면 된다. 
  * 테스트 오라클 (The test oracle)
    * 어떤 알고리즘은 구현이 여러 개 있을 수 있다. 구현한 고성능 알고리즘은 더 간단한 알고리즘으로 테스트할 수 있다.
    * 병렬 처리로 계산 된 것도 그냥 순서대로 계산하는 로직으로 확인 할 수 있다.

### mkTest 테스트 해보기

* `Task` 자동 생성하기
* 자동 생성 샘플 타입은 `Arbitrary` 타입 클래스 인스턴스여야 한다.
* 기본 타입에 대한 `Arbitrary` 인스턴스는 만들어져 있다.
* `quickcheck-instances`에 자주 쓰는 추가 타입(UTCTime 같은...)에 대한 `Arbitrary` 인스턴스가 있다.

```haskell
import Test.QuickCheck.Instances ()
```

* `Arbitrary` 인스턴스 직접 만들기 

```haskell
newtype ValidContent = ValidContent Text deriving (Show, Eq)

instance Arbitrary ValidContent where
  arbitrary =
    ValidContent
      <$> arbitrary
      `suchThat` (\x -> Text.length x >= 0 && Text.length x <= 13)
```

```haskell
instance Arbitrary ULID where
  arbitrary = do
    let n :: Gen Integer = fromIntegral <$> chooseInt (0, maxBound :: Int)
    fromRight (error "Invalid ULID integer") . ulidFromInteger <$> n
```

* quickcheck-string-random 패키지에 regex 기반으로 문자열을 생성하는 `matchRegexp` 함수가 있다

```haskell
import Test.QuickCheck.StringRandom (matchRegexp)

newtype ValidEmailText = ValidEmailText Text deriving (Eq, Show)

instance Arbitrary ValidEmailText where
  arbitrary = ValidEmailText <$> matchRegexp "[A-Z0-9._%+-]+@[A-Z0-9.-]+\\.[A-Z]{2,4}"
```

## 테스트 더블로 유스케이스 테스트하기

* 데이터베이스 픽스처 만들기 
  * https://github.com/eunmin/realworld-haskell/blob/main/test/RealWorld/Domain/Command/Fixture.hs
  * https://github.com/eunmin/realworld-haskell/blob/main/test/RealWorld/Domain/Command/User/UseCaseSpec.hs
* TestApp에 테스트 더블 연결하기 
  * https://github.com/eunmin/realworld-haskell/blob/main/test/RealWorld/Domain/Command/TestApp.hs

## 배포

* `stack path` 명령어로 빌드 결과 바이너리 위치를 알 수 있다.

```sh
$ stack build
$ stack path --local-install-root
```

* 바이너리를 도커 복사해 이미지를 만들면 된다.
  * 도커에서 빌드 vs 머신에서 빌드

## 못 다한 1/10
* 타입 레벨 (term, type, kind 세상, Data Kinds, Type-level literal)
* 하스켈 고급 타입 기능 (GADT, Type Family)
* RankNTypes
* 하스켈 메타 프로그래밍
* 성능 측정과 모니터링(ekg, wai-middleware-metrics, resource-pool 커스텀 구현), 로깅(wai-extra)
  ```haskell
  main :: IO ()
  main = do
  withConnection $ \conn -> do
    store <- serverMetricStore <$> forkServer "localhost" 8000
    waiMetrics <- registerWaiMetrics store
    scottyT 3000 (runner conn) $ do
      middleware (metrics waiMetrics)
      middleware logStdout

      get "/tasks" $ html "Hello World"
      post "/tasks" Task.createTask
  ```
* 배운 것은 3/10 정도, 하지만 4/10 정도면 현실 애플리케이션 개발을 할 수 있다.