
# 하스켈 배움터 4일

#### 실습 20
앞에 실습에서 `r` 값을 사용하는 `Config r a` 값을 만들어보라.

#### 실습 21

다음과 같이 `Config` 타입에 펑터 인스턴스를 만들어 보자. `Config` 타입의 펑터는 일반 값인 `a`를 꺼내서 `f`를 적용하고
다시 `Config`로 감싸주는 함수다. 인스턴스에 시그니처를 쓰기 위해 `{-# LANGUAGE InstanceSigs #-}` 언어 확장을 추가했다.

```haskell
instance Functor (Config r) where
  fmap :: (a -> b) -> Config r a -> Config r b
  fmap f x = undefined
```

#### 실습 22

`Config Int String` 값을 만들고 펑터에 `<$>`를 이용해서 일반 함수 `("Hello " ++)`를 적용해 최종 결과인 `a` 값을
꺼내보라.

`Config`의 모나드 인스턴스를 만들어보자. 앞에서 본 것 처럼 모나드 인스턴스가 되려면 `Applicative` 인스턴스가 필요하다.
다음은 `Config`의 `Applicative` 인스턴스다.

```haskell
instance Applicative (Config r) where
  pure x = Config $ \_ -> x
  (<*>) :: Config r (a -> b) -> Config r a -> Config r b
  (<*>) (Config f) (Config x) = Config $ \r -> f r (x r)
```

다음은 `Applicative`를 사용하는 예이다.

```haskell
result2 :: Config Int String
result2 = (++) <$> Config (\r -> show (r + 1)) <*> Config (\r -> show (r + 2))

-- >>> runConfig result2 1
-- "23"
```

펑터와 어플리커티브에서 `r` 값은 `Config` 안에 있는 계산에서만 쓸 수 있었다. 모나드는 계산 과정을 중첩할 수 있기 때문에 
`Config`의 어떤 계산 과정에서도 `r` 값을 쓸 수 있을 것이다.

#### 실습 23

`Config`에 대한 모나드 인스턴스를 구현하라

```haskell
instance Monad (Config r) where
  return = pure
  (>>=) :: Config r a -> (a -> Config r b) -> Config r b
  (>>=) = undefined
```

`Config` 모나드로 계산을 이어 붙이면 계산 과정 중 필요하다면 언제든 `r` 값을 쓸 수 있다.

```haskell
result3 :: Config Int String
result3 = Config (\r -> 42) >>= \x -> Config (\r -> r) >>= \config -> pure $ "Result: " ++ show (x + config)

-- >>> runConfig result3 1
-- "Result: 43"
```

`do` 구문으로 쓰면 다음과 같다.

```haskell
result4 :: Config Int String
result4 = do
  x <- pure 42
  config <- Config (\r -> r)
  pure $ "Result: " ++ show (x + config)

-- >>> runConfig result4 1
-- "Result: 43"
```

하스켈에서는 이 `Config` 모나드의 구현을 `Reader`라고 한다.

### 의존성 추가하기

하스켈에서는 `Reader` 모나드가 `mtl` 또는 `transformers` 패키지에 구현되어 있다.
의존성을 추가하기 위해 `package.yaml` 파일을 열어 `mtl` 패키지를 다음과 같이 추가한다.

```yaml
dependencies:
- base >= 4.7 && < 5
- mtl
```

패키지를 추가하고 VSCode에서 활성화 하려면 VScode를 재시작하거나 `Haskell: Restart LSP Server` 명령어로 `HLS`를
재시작해주면 된다.

`mtl` 패키지에 `Reader` 모나드는 `Control.Monad.Reader` 모듈에 있다.

```haskell
import Control.Monad.Reader (Reader, ask, runReader)
```

`Reader r a` 타입은 실습에서 만든 것처럼 `r` 타입을 `ask` 함수로 모나드 안에서 가져올 수 있다.

```haskell
example :: Reader Int String
example = do
  x <- ask
  pure $ "Value: " ++ show x
```

### 실습 24

다음 `app` 값을 Bool 타입의 `isDeveMode` 값을 전역 값 대신 `Reader` 모나드로 읽어오도록 코드를 고쳐라

```haskell
isDevMode = True

app :: Int 
app = 
  result
  where 
    x = 40
    y = if isDevMode then 0 else 2
    result = x + y
```

### Writer

`Reader`와 비슷하지만 맥락에 값을 기록 할 수 있는 `Write` 모나드가 있다. `tell` 함수로 기록 한다.

```haskell
import Control.Monad.Writer (Writer, tell, runWriter)

app :: Writer [String] Int
app = do
    let x = 40
    let y = 2
    tell ["y=" ++ show y]
    return $ x + y
```

기록 한 값은 `runWriter`를 했을 때 결과와 함께 나온다.

```haskell
-- >>> (result, logs) = runWriter app
```

### State

`Reader`와 `Writer` 기능을 함께 갖고 있는 `State` 모나드가 있다. 모나드 안에서 상태가 있는 값을 흉내낼 수 있다.

```haskell
import Control.Monad.State (State, get, put, runState)

app :: State Int Int
app = do
  let x = 40
  value <- get
  put $ value + 1
  value <- get
  put $ value + 1
  value <- get
  return $ x + value

-- >>> (result, state) = runState app 0
```

### IO

입출력을 담당하는 `IO`도 모나드이다. `IO` 모나드가 하는 일은 거의 없다. `IO`는 모나드 맥락에서 부수 효과를 모으는 역할을
한다. 부수 효과를 모두 모아서 `IO` 맥락에 담아서 `main` 함수에 전달하면 `main` 함수는 부수 효과를 실제로 발생시키는 역할을 
한다.

```haskell
import System.Environment (getArgs)

app :: IO Int
app = do
  (x:_) <- getArgs -- 커맨드 라인 입력 값을 문자열 리스트로 가져오는 부수 효과를 모아 둔다.
  let y = "Arg: " ++ x
  _ <- print y -- 화면에 출력하는 부수효과를 모은다. print의 리턴 타입은 IO () 이다.
  pure 0

main :: IO () -- IO 모나드로 모은 모든 부수효과를 순서대로 처리한다
main = do 
  x <- app
  print x -- x 값을 화면에 출력하는 부수 효과를 모은다
```



### 실습 15

`ages`에서 아이디 두 개를 받아서 나이를 합치는 `sumAges` 함수를 작성하라

```haskell
sumAges :: Int -> Int -> Maybe Int
```


## 어플리커티브

위 실습 15에서 `sumAges`는 사실 앞의 계산과 뒤의 계산이 독립적이기 때문에 꼭 모나드를 쓸 필요가 없다. 이런 경우 어플리커티브
펑터를 사용하면 좋다. 다음은 `Applicative` 타입 클래스에 있는 `apply`라고 부르는 함수다.

```haskell
class Applicative f where
  (<*>) :: f (a -> b) -> f a -> f b
```

펑터와 비교해 봅시다.

```haskell
(<$>) :: (a -> b) -> f a -> f b
```

비슷하지만 일반 함수를 `f`로 감싼 타입입니다. 그렇다면 사용은 다음과 같이 할 수 있다.

```haskell
-- >>> (+1) <$> Just 1
-- Just 2

-- >>> Just (+1) <*> Just 1
-- Just 2
```

직관적으로 이해하기 어렵지만, 기억하면 좋을 것은 일반 함수를 쓰는데 인자 값들이 감싼 타입일 때 쓰면 좋다. `sumAges`는
`(+)`를 적용하는데 인자가 `Maybe Int`였기 때문에 어플리커티브를 쓰기 적절하다. 일반 함수를 `f (a -> b)`로 만들 때
보통 첫번째 인자에 펑터를 적용해서 다음과 같이 사용할 수 있다.

```haskell
sumAges :: Int -> Int -> Maybe Int
sumAges id1 id2 :: (+) <$> lookup id1 ages <*> lookup id2 ages
```

차근차근 보면 다음과 같다. `(+) <$> lookup id1 ages`는 무슨 타입인가? `\x y -> x + y <$> Maybe Int`일 것이다.
`Int` 타입은 `x`에 들어간다면 결과는 `x`가 부분 적용된 함수이다. 펑터의 결과는 일반 함수에 다시 `f`로 감싼 것이기 때문에 
결과 `(+)`의 나머지 인자를 받는 함수에 `Maybe`가 씌워진 `Maybe (Int -> Int)` 타입이 된다.

어플리커티브 타입 클래스에 많이 쓰는 함수 중 하나는 `pure`이다. `pure`는 모나드의 `return`과 같다. 많은 사람들이 `return`
보다 `pure`를 선호하는 것 같다. 이것이 조금 더 일반적인 버전이기 때문같다. 왜냐하면 `Monad`가 되려면 타입 생성자가 
`Applicative`여야 하는 제약이 있기 때문이다.

```haskell
class Applicative m => Monad m where
  (>>=) :: m a -> (a -> m b) -> m b
  return :: a -> m a
  ...
```

또 `Applicative`는 `Fuctor` 인스턴스가 있어야하는 제약이 있다.

```haskell
class Functor f => Applicative f where
  pure :: a -> f a
  (<*>) :: f (a -> b) -> f a -> f b
  ...
```

`Functor`는 다른 제약은 없다.

```haskell
class Functor f where
  fmap :: (a -> b) -> f a -> f b
  ...
```

## 세미그룹

세미그룹은 효과와 직접적으로 관계는 없는 타입 클래스입니다. `Semigroup` 타입 클래스는 뭔가 합칠 수 있는 것들에 대한 추상입니다.
대표적인 인스턴스로 리스트 타입이 있습니다. 

```haskell
class Semigroup m where
  (<>) :: m -> m -> m
  ...
```

`String`도 리스트이기 때문에 다음과 같이 합칠 수 있습니다.

```haskell
-- >>> "Hello" <> " " <> "World"
-- "Hello World"
```

## 모노이드

모노이드는 세미그룹에 `mempty`가 있는 형태입니다. 세미그룹과 활용하여 `mempty`를 초기 값으로 활요할 수 있습니다.
`mappend`는 `(<>)`와 같지만 보통 `(<>)`를 많이 씁니다. 

```haskell
class Semigroup m => Monoid m where
  mempty :: m
  mappend :: a -> a -> a
```

리스트이 `mempty`는 `()` 빈 리스트입니다. 

```haskell
value :: [Int]
value = mempty [1, 2, 3]

-- >>> value
-- []
```

## Traverse

다음 값이 있을 때,

### 실습 16

```haskell
ids :: [(String, Int)]
ids = [("John", 1), ("Jane", 2), ("Jim", 3)]

names :: [String]
names = ["John", "Jane", "Jim"]
```

다음 함수를 만들어라

```haskell
ages :: [Maybe Int]
```

### 실습 17

그리고 다음 함수에 만들어보시오.

```haskell
ages' :: Maybe [Int]
```