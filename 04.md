
# 하스켈 배움터 4일

## 시작하기 전에

`pacakge.yaml`에 다음과 같은 의존성을 추가하고 `stack build`하기

```yaml
dependencies:
- transformers
- mtl
- monad-logger
- text
```

## 복습

### Functor

```haskell
class Functor f where
	fmap :: (a -> b) -> f a -> f b 
  (<$>) :: (a -> b) -> f a -> f b
```

* 어떤 함수와 펑터 타입을 받아 함수를 적용한다.
* `fmap`보다 중위 함수인 `(<$>)`를 더 많이 쓴다.

### Monad

```haskell
class Monad m where
  (>>=) :: m a -> (a -> m b) -> m b
  return :: a -> m a
```

* 모나드 값과 함수를 받아 함수를 적용한 모나드 값을 리턴한다. 인자 함수는 모나드 값을 리턴해야 한다.
* 안 쪽 함수에 다시 `(>>=)`를 써서 모나드를 연속해서 쓸 수 있다.
* `return`은 일반 값을 모나드 값으로 만든다. 모든 모나드는 어플리커티브이기 때문에 `pure`를 더 많이 쓴다.
* 모나드를 쉽게 쓸 수 있는 `do` 구문이 있다.
  ```haskell
  app = do
    심볼 <- 모나드 값
    ...
    심볼 <- 모나드 값
    모나드 값
  ```

### Maybe 

```haskell
data Maybe a = Just a | Nothing
```

* 계산 결과 값이 없을 때를 표현할 때 주로 쓴다.
* 모나드 인스턴스가 있다.

```haskell
phones :: [(String, String)]
phones = [("123-123-123", "Todd"), ("000-000-000", "Eunmin")]

ids :: [(String, Int)]
ids = [("Todd", 32), ("Eunmin", 424)]

ages :: [(Int, Int)]
ages = [(32, 100), (42, 50)]

getAgeByPhone :: String -> Maybe Int
getAgeByPhone phone = do
  name <- lookup phone phones
  id <- lookup name ids
  age <- lookup id ages
  return age
```

### Either

```haskell
data Either a b = Left a | Right b
```

* 왼쪽에 에러 타입을 오른쪽에 결과 타입으로 계산에 실패 한 경우 왼쪽에 실패 내용을 담아 준다.
* 모나드 인스턴스가 있다.


```haskell
import Data.Aeson (eitherDecode)

eitherDecode :: String -> Either String a
```

```haskell
value :: Either String Int
value = do
  x <- eitherDecode "1"
  y <- eitherDecode "2"
  pure $ x + y
```

### 4일차 내용

### Config

```haskell
data Config r a = Config {x :: r -> a}

getValue :: Config String Int -> Int
getValue (Config f) = f ""

-- >>> getValue $ Config $ \x -> 42

-- >>> x (Config $ \x -> 1) ""
```

### newtype에 대한 스타일 가이드

* 값 생성자가 하나고 필드가 하나라면 `newtype`으로 쓸 수 있다.

```haskell
data Config r a = Config {x :: r -> a}
```

* `newtype`의 필드명은 보통 `un타입이름` 또는 `run타입이름`으로 쓴다.

```haskell
data Config r a = Config {runConfig :: r -> a}
```

#### 실습

다음과 같이 `Config` 타입에 펑터 인스턴스를 만들어 보자. `Config` 타입의 펑터는 일반 값인 `a`를 꺼내서 `f`를 적용하고
다시 `Config`로 감싸주는 함수다. 인스턴스에 시그니처를 쓰기 위해 `{-# LANGUAGE InstanceSigs #-}` 언어 확장을 추가했다.

```haskell
instance Functor (Config r) where
  fmap :: (a -> b) -> Config r a -> Config r b
  fmap f x = undefined
```

```haskell
value :: Config Int Integer
value = fmap (+ 1) (Config $ \_ -> 42)

-- >>> runConfig value 100
-- 43
```

* `r` 값으로 아무 값이나 넣었지만 사실 `Config` 함수 안에서 활용할 수 있다.

```haskell
value2 :: Config Int Integer
value2 = fmap (+ 1) (Config $ \r -> 42 + r)

-- >>> runConfig value2 100
-- 143
```

* `Config`의 Functor는 크게 쓸모있어 보이지 않는다. 하지만 `Config`의 모나드는 유용하다. 다음은 `Config`의 하스켈 구현인
  `Reader`에 대해 알아보자.

## Reader 모나드

### 의존성 추가하기

하스켈에서는 `Reader` 모나드가 `transformers` 패키지에 구현되어 있다.
의존성을 추가하기 위해 `package.yaml` 파일을 열어 `transformers` 패키지를 다음과 같이 추가한다.

```yaml
dependencies:
- base >= 4.7 && < 5
- transformers
```

패키지를 추가하고 VSCode에서 활성화 하려면 VScode를 재시작하거나 `Haskell: Restart LSP Server` 명령어로 `HLS`를
재시작해주면 된다. 첫 컴파일은 오래 걸릴 수 있고 `LSP`를 다시 시작하는 것 보다. 터미널에서 `stack build` 하면 빌드 과정이
나와서 덜 답답할 수 있다.

```haskell
import Control.Monad.Trans.Reader (Reader, ask, runReader)
```

`Reader r a` 타입은 `Config와 비슷하다` 만든 것처럼 `r` 타입을 `ask` 함수로 모나드 안에서 가져올 수 있다.

```haskell
example :: Reader Int String
example = do
  x <- ask
  pure $ "Value: " ++ show x
```

* Config의 예에서 `ask`는 그냥 `r` 값을 그대로 주는 함수와 같다.

```haskell
ask :: Config r r
ask = Config $ \r -> r
```

### 실습 

다음 `app` 값을 Bool 타입의 `isDeveMode` 값을 전역 값 대신 `Reader` 모나드로 읽어오도록 코드를 고쳐라

```haskell
isDevMode = True

app :: Int 
app = 
  result
  where 
    x = 40
    y = if isDevMode then 0 else 2
    result = x + y
```

### Writer

`Reader`와 비슷하지만 맥락에 값을 기록 할 수 있는 `Write` 모나드가 있다. `tell` 함수로 기록 한다.

```haskell
import Control.Monad.Trnas.Writer (Writer, tell, runWriter)

app :: Writer [String] Int
app = do
    let x = 40
    let y = 2
    tell ["y=" ++ show y]
    return $ x + y
```

기록 한 값은 `runWriter`를 했을 때 결과와 함께 나온다.

```haskell
-- >>> (result, logs) = runWriter app
```

### State

`Reader`와 `Writer` 기능을 함께 갖고 있는 `State` 모나드가 있다. 모나드 안에서 상태가 있는 값을 흉내낼 수 있다.

```haskell
import Control.Monad.State (State, get, put, runState)

app :: State Int Int
app = do
  let x = 40
  value <- get
  put $ value + 1
  value <- get
  put $ value + 1
  value <- get
  return $ x + value

-- >>> (result, state) = runState app 0
```

### IO

입출력을 담당하는 `IO`도 모나드이다. `IO` 모나드가 하는 일은 거의 없다. `IO`는 모나드 맥락에서 부수 효과를 모으는 역할을
한다. 부수 효과를 모두 모아서 `IO` 맥락에 담아서 `main` 함수에 전달하면 `main` 함수는 부수 효과를 실제로 발생시키는 역할을 
한다.

```haskell
import System.Environment (getArgs)

app :: IO Int
app = do
  (x:_) <- getArgs -- 커맨드 라인 입력 값을 문자열 리스트로 가져오는 부수 효과를 모아 둔다.
  let y = "Arg: " ++ x
  _ <- print y -- 화면에 출력하는 부수효과를 모은다. print의 리턴 타입은 IO () 이다.
  pure 0

main :: IO () -- IO 모나드로 모은 모든 부수효과를 순서대로 처리한다
main = do 
  x <- app
  print x -- x 값을 화면에 출력하는 부수 효과를 모은다
```

## Monad Transformer

* 여러 개의 모나드 효과를 하나의 모나드로 합쳐서 사용하기
* `transformers` 패키지에 정의

### ReaderT

* `Reader`와 다른 모나드를 합칠 수 있음

```haskell
data ReaderT r m a = ...
```

* `m`은 합칠 다른 모나드
* 안 쪽 모나드를 바깥 쪽 모나드로 바꾸기 위해 `lift`

```haskell
class MonadTrans t where
  lift :: Monad m => m a -> t m a
```

```haskell
import Control.Monad.Trans.Class (lift)
import Control.Monad.Trans.Reader (ReaderT, ask, runReaderT)

app :: ReaderT Int IO Int
app = do
  x <- ask
  y <- lift readLn
  pure $ x + y

main :: IO ()
main = do
  result <- runReaderT app 40
  print result
```

### 하나 더 쌓기 

* 한 칸 올리려면 `lift`, 두 칸 올리려면 `lift $ lift`

```haskell
import Control.Monad.Trans.Class (lift)
import Control.Monad.Trans.Reader (ReaderT, ask, runReaderT)
import Control.Monad.Trans.Writer (WriterT, runWriterT, tell)

app :: ReaderT Int (WriterT String IO) Int
app = do
  x <- ask
  y <- lift $ lift readLn
  lift $ tell $ "y: " <> show y
  pure $ x + y

main :: IO ()
main = do
  let writer = runReaderT app 40
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

* 특별히 `IO` 모나드는 `lift` 중첩 대신 `liftIO`로 한 번에 올릴 수 있다.

```haskell
import Control.Monad.IO.Class (liftIO)
import Control.Monad.Trans.Class (lift)
import Control.Monad.Trans.Reader (ReaderT, ask, runReaderT)
import Control.Monad.Trans.Writer (WriterT, runWriterT, tell)

app :: ReaderT Int (WriterT String IO) Int
app = do
  x <- ask
  y <- liftIO readLn
  lift $ tell $ "y: " <> show y
  pure $ x + y

main :: IO ()
main = do
  let writer = runReaderT app 40
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

### 실습

* `WriterT` 대신 `monad-logger` 패키지를 이용해서 로그를 남겨보시오.
  * https://hackage-content.haskell.org/package/monad-logger-0.3.42

#### 준비물
* `package.yaml` `dependencies`에 `monad-logger`, `text` 추가
* `{-# LANGUAGE OverloadedStrings #-}` 언어 확장 사용

```haskell
newtype LoggingT m a = ...

runLoggingT :: LoggingT m a -> (Loc -> LogSource -> LogLevel -> LogStr -> IO ()) -> m a

defaultOutput :: Handle -> (Loc -> LogSource -> LogLevel -> LogStr -> IO ())

logInfoN :: Text -> m ()
``` 

```haskell
{-# LANGUAGE OverloadedStrings #-}

import System.IO (stdout)
import Data.Text (pack)

stdout :: Handle

-- >>> logInfoN $ "y: " <> pack (show y)

-- >>> runLoggingT app (defaultOption stdout)
```

### OverloadedStrings

* 문자열 리터럴 `""`는 String 타입(`[Char]`)이지만 ByteString, Text와 같은 다른 타입도 문자열 리터럴 `""`를 쓸 수
있게 해주는 언어 확장, 내부적으로 `IsString` 타입 클래스의 `fromString :: String -> a`가 구현되어 있어야 한다.

```haskell
{-# LANGUAGE OverloadedStrings #-}
import Data.Text

a :: String
a = "Hello"

b :: Text
b = "Hello"
```

## mtl 스타일

* 모나드 트랜스포머는 mtl 패키지에도 있다. 
* mtl 패키지에 있는 모나드 트랜스포머는 조금 다른 방식으로 사용한다.
* `transformer`와 비교해보자.

```haskell
import Control.Monad.IO.Class (liftIO)
import Control.Monad.Reader (ReaderT, ask, runReaderT) -- 모듈이 다르다.
import Control.Monad.Writer (WriterT, runWriterT, tell)

app :: ReaderT Int (WriterT String IO) Int
app = do
  x <- ask
  y <- liftIO readLn
  tell $ "y: " <> show y -- lift를 하지 않는다.
  pure $ x + y

main :: IO ()
main = do
  let writer = runReaderT app 40
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

* `ReaderT` 안에서 `tell`을 그대로 썼다. 어떻게 된 것인가? `tell` 정의를 보자.

```haskell
class (Monoid w, Monad m) => MonadWriter w m | m -> w where
  tell :: w -> m ()
```

* `tell`은 꼭 `Writer` 모나드가 아니고 `m`에서 쓸 수 있다. `m`은 `Monad m`과 `MonadWriter w m` 인스턴스가 있으면
  된다. 그렇다면 `ReaderT Int (WriterT String IO) Int`는 `Monad m`과 `MonadWriter w m` 인스턴스가 있을까?
  `MonadWriter` 인스턴스 목록을 보면 `MonadWriter w m => MonadWriter w (ReaderT r m)`, `ReaderT`에 대한
  구현이 있다. `MonadWriter`는 `tell`을 구현해야 하기 때문에 다음과 같이 `ReaderT`에 대한 `tell`이 구현되어 있다.

  ```haskell
  instance MonadWriter w m => MonadWriter w (ReaderT r m) where
    tell :: w -> ReaderT r m ()
    tell = ...
  ```
  * 그래서 `tell`을 `ReaderT`에서 바로 쓸 수 있었다.

* 사실 `mtl` 스타일은 원래의 모나드 트랜스포머는 아니지만 어떤 모나드 문맥에서 여러 모나드를 타입 클래스 제약으로 쓸 수 있다.
* 그래서 앞에 모나드 트랜스포머 예제는 다음과 같이 쓸 수 있다.

```haskell
{-# LANGUAGE FlexibleContexts #-}

import Control.Monad.IO.Class (MonadIO, liftIO)
import Control.Monad.Reader (MonadReader, ask, runReaderT)
import Control.Monad.Writer (MonadWriter, runWriterT, tell)

app :: (MonadReader Int m, MonadWriter String m, MonadIO m) => m Int
app = do
  x <- ask
  y <- liftIO readLn
  tell $ "y: " <> show y
  pure $ x + y

main :: IO ()
main = do
  let writer = runReaderT app 40
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

* 하스켈 타입 추론에 따라 `m`은 결국 `ReaderT Int (WriterT String IO) Int`로 추론 된다.
* `monad-logger` 패키지도 `mtl` 스타일을 지원하기 때문에 다음과 같이 쓸 수 있다.

```haskell
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE OverloadedStrings #-}

import Control.Monad.IO.Class (MonadIO, liftIO)
import Control.Monad.Logger (MonadLogger, defaultOutput, logInfoN, runLoggingT)
import Control.Monad.Reader (MonadReader, ask, runReaderT)
import Data.Text (pack)
import System.IO (stdout)

app :: (MonadReader Int m, MonadLogger m, MonadIO m) => m Int
app = do
  x <- ask
  y <- liftIO readLn
  logInfoN $ "y: " <> pack (show y)
  pure $ x + y

main :: IO ()
main = do
  let logger = runReaderT app 40
  result <- runLoggingT logger (defaultOutput stdout)
  print result
```

* 그럼 뭐가 더 좋은가?

### mtl의 유연함

다음과 같은 타입 클래스가 있다고 하자.

```haskell
class UserRepository m where
  findNameById :: Int -> m String
```

그리고 예제에서 숫자 `y`를 `userId`라고 하고 `m`이 `UserRepository` 제약을 갖는다면 다음과 같이 쓸 수 있다.

```haskell
app :: (MonadReader Int m, MonadWriter String m, MonadIO m, UserRepository m) => m Int
app = do
  x <- ask
  userId <- liftIO readLn
  userName <- findNameById userId
  tell $ "userId: " <> show userId <> ", userName: " <> userName
  pure $ x + userId

main :: IO ()
main = do
  let writer = runReaderT app 40 -- 컴파일 에러!!!
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

* `app`은 `ReaderT Int (WriterT String IO) Int`로 추론되는데 이 타입은 `UserRepository` 인스턴스가 없기 때문에
  컴파일이 안 된다.
* `ReaderT Int (WriterT String IO) Int` 타입을 갖는 `App` 타입을 만들어 `UserRepository` 인스턴스를 만들 수 있다.

```haskell
newtype App a = App {unApp :: ReaderT Int (WriterT String IO) a}

app :: (MonadReader Int m, MonadWriter String m, MonadIO m, UserRepository m) => m Int
app = do
  x <- ask
  userId <- liftIO readLn
  userName <- findNameById userId
  tell $ "userId: " <> show userId <> ", userName: " <> userName
  pure $ x + userId

main :: IO ()
main = do
  let writer = runReaderT app 40 -- 컴파일 에러!!!
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

* 이제 `app` 값의 타입은 `m Int`이기 때문에 `unApp :: App a -> ReaderT ...`에 넘기면 `m`은 `App`으로 추론된다.

```haskell
newtype App a = App {unApp :: ReaderT Int (WriterT String IO) a}

app :: (MonadReader Int m, MonadWriter String m, MonadIO m, UserRepository m) => m Int
app = do
  x <- ask
  userId <- liftIO readLn
  userName <- findNameById userId
  tell $ "userId: " <> show userId <> ", userName: " <> userName
  pure $ x + userId

main :: IO ()
main = do
  let writer = runReaderT (unApp app) 40 -- 컴파일 에러!!!
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

* 하지만 컴파일 에러가 난다. 왜냐하면 `App`에는 `MonadReader`, `MonadWriter` 같은 인스턴스가 없기 때문이다.
* 다행히 `deriving`으로 `GeneralizedNewtypeDeriving`으로 다음과 같이 인스턴스를 만들어 줄 수 있다.

```haskell
newtype App a
  = App {unApp :: ReaderT Int (WriterT String IO) a}
  deriving (Functor, Applicative, Monad, MonadReader Int, MonadWriter String, MonadIO)
```

* 이제 `App`은 `MonadReader` 등의 인스턴스가 있고 마지막으로 `UserRepository` 인스턴스만 만들면 된다.

```haskell
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE GeneralizedNewtypeDeriving #-}

import Control.Monad.IO.Class (MonadIO, liftIO)
import Control.Monad.Reader (MonadReader, ReaderT, ask, runReaderT)
import Control.Monad.Writer (MonadWriter, WriterT, runWriterT, tell)

class UserRepository m where
  findNameById :: Int -> m String

newtype App a
  = App {unApp :: ReaderT Int (WriterT String IO) a}
  deriving (Functor, Applicative, Monad, MonadReader Int, MonadWriter String, MonadIO)

instance UserRepository App where
  findNameById _ = pure "Eunmin"

app :: (MonadReader Int m, MonadWriter String m, MonadIO m, UserRepository m) => m Int
app = do
  x <- ask
  userId <- liftIO readLn
  userName <- findNameById userId
  tell $ "userId: " <> show userId <> ", userName: " <> userName
  pure $ x + userId

main :: IO ()
main = do
  let writer = runReaderT (unApp app) 40
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

* 만약 UserRepository의 다른 구현을 쓰고 싶다면 `App` 대신 다른 타입을 만들고 구현을 바꾸면 된다.

```haskell
{-# LANGUAGE GeneralizedNewtypeDeriving #-}

import Control.Monad.IO.Class (MonadIO, liftIO)
import Control.Monad.Reader (MonadReader, ReaderT, ask, runReaderT)
import Control.Monad.Writer (MonadWriter, WriterT, runWriterT, tell)

class UserRepository m where
  findNameById :: Int -> m String

newtype App a
  = App {unApp :: ReaderT Int (WriterT String IO) a}
  deriving (Functor, Applicative, Monad, MonadReader Int, MonadWriter String, MonadIO)

instance UserRepository App where
  findNameById _ = pure "Eunmin"

newtype TestApp a
  = TestApp {unTestApp :: ReaderT Int (WriterT String IO) a}
  deriving (Functor, Applicative, Monad, MonadReader Int, MonadWriter String, MonadIO)

instance UserRepository TestApp where
  findNameById _ = pure "Test Eunmin"

app :: (MonadReader Int m, MonadWriter String m, MonadIO m, UserRepository m) => m Int
app = do
  x <- ask
  userId <- liftIO readLn
  userName <- findNameById userId
  tell $ "userId: " <> show userId <> ", userName: " <> userName
  pure $ x + userId

main :: IO ()
main = do
  let writer = runReaderT (unTestApp app) 40
  (result, logs) <- runWriterT writer
  putStrLn logs
  print result
```

`app` 구현은 그대로 두고 `findNameById`를 다른 구현으로 바꿀 수 있다.

### FlexibleContexts

타입 제약에는 원래 타입 변수만 쓸 수 있지만 `FlexibleContexts`를 사용하면 특정 타입으로 제한할 수 있다.

```haskell
app :: (MonadReader r m) => m Int
```

* `MonadReader`에 타입 변수 두 개는 타입 변수만 쓸 수 있지만 `FlexibleContexts` 언어 확장이 있으면 다음과 같이 표시
  할 수 있다.

```haskell
app :: (MonadReader Int m) => m Int
```

### GeneralizedNewtypeDeriving

`newtype`은 안에 있는 타입이 하나 밖에 없다. 아래 예는 `Int`이다. 

```haskell
newtype MyInt = MyInt Int
```

`GeneralizedNewtypeDeriving` 언어 확장은 `newtype`을 파생할 때 안에 있는 타입의 기본 구현으로 만들어 준다.

```haskell
{-# LANGUAGE GeneralizedNewtypeDeriving #-}

newtype MyInt = MyInt Int deriving (Num)
```

`Num MyInt`는 자동으로 구현할 수 없지만 `Num Int`라고 하면 자동 구현을 만들 수 있다.

## 어플리커티브

### 실습

`ages`에서 아이디 두 개를 받아서 나이를 합치는 `sumAges` 함수를 작성하라

```haskell
ages :: [(Int, Int)]
ages = [(32, 100), (42, 50)]

sumAges :: Int -> Int -> Maybe Int
sumAges = undefined
```

위 실습에서 `sumAges`는 사실 앞의 계산과 뒤의 계산이 독립적이기 때문에 꼭 모나드를 쓸 필요가 없다. 이런 경우 어플리커티브
펑터를 사용하면 좋다. 다음은 `Applicative` 타입 클래스에 있는 `apply`라고 부르는 함수다.

```haskell
class Applicative f where
  (<*>) :: f (a -> b) -> f a -> f b
```

펑터와 비교해 봅시다.

```haskell
(<$>) :: (a -> b) -> f a -> f b
```

비슷하지만 일반 함수를 `f`로 감싼 타입입니다. 그렇다면 사용은 다음과 같이 할 수 있다.

```haskell
-- >>> (+1) <$> Just 1
-- Just 2

-- >>> Just (+1) <*> Just 1
-- Just 2
```

직관적으로 이해하기 어렵지만, 기억하면 좋을 것은 일반 함수를 쓰는데 인자 값들이 감싼 타입일 때 쓰면 좋다. `sumAges`는
`(+)`를 적용하는데 인자가 `Maybe Int`였기 때문에 어플리커티브를 쓰기 적절하다. 일반 함수를 `f (a -> b)`로 만들 때
보통 첫번째 인자에 펑터를 적용해서 다음과 같이 사용할 수 있다.

```haskell
sumAges :: Int -> Int -> Maybe Int
sumAges id1 id2 :: (+) <$> lookup id1 ages <*> lookup id2 ages
```

차근차근 보면 다음과 같다. `(+) <$> lookup id1 ages`는 무슨 타입인가? `\x y -> x + y <$> Maybe Int`일 것이다.
`Int` 타입은 `x`에 들어간다면 결과는 `x`가 부분 적용된 함수이다. 펑터의 결과는 일반 함수에 다시 `f`로 감싼 것이기 때문에 
결과 `(+)`의 나머지 인자를 받는 함수에 `Maybe`가 씌워진 `Maybe (Int -> Int)` 타입이 된다.

어플리커티브 타입 클래스에 많이 쓰는 함수 중 하나는 `pure`이다. `pure`는 모나드의 `return`과 같다. 많은 사람들이 `return`
보다 `pure`를 선호하는 것 같다. 이것이 조금 더 일반적인 버전이기 때문같다. 왜냐하면 `Monad`가 되려면 타입 생성자가 
`Applicative`여야 하는 제약이 있기 때문이다.

```haskell
class Applicative m => Monad m where
  (>>=) :: m a -> (a -> m b) -> m b
  return :: a -> m a
  ...
```

또 `Applicative`는 `Fuctor` 인스턴스가 있어야하는 제약이 있다.

```haskell
class Functor f => Applicative f where
  pure :: a -> f a
  (<*>) :: f (a -> b) -> f a -> f b
  ...
```

`Functor`는 다른 제약은 없다.

```haskell
class Functor f where
  fmap :: (a -> b) -> f a -> f b
  ...
```

## 세미그룹

세미그룹은 효과와 직접적으로 관계는 없는 타입 클래스입니다. `Semigroup` 타입 클래스는 뭔가 합칠 수 있는 것들에 대한 추상입니다.
대표적인 인스턴스로 리스트 타입이 있습니다. 

```haskell
class Semigroup m where
  (<>) :: m -> m -> m
  ...
```

`String`도 리스트이기 때문에 다음과 같이 합칠 수 있습니다.

```haskell
-- >>> "Hello" <> " " <> "World"
-- "Hello World"
```

## 모노이드

모노이드는 세미그룹에 `mempty`가 있는 형태입니다. 세미그룹과 활용하여 `mempty`를 초기 값으로 활요할 수 있습니다.
`mappend`는 `(<>)`와 같지만 보통 `(<>)`를 많이 씁니다. 

```haskell
class Semigroup m => Monoid m where
  mempty :: m
  mappend :: a -> a -> a
```

리스트이 `mempty`는 `()` 빈 리스트입니다. 

```haskell
value :: [Int]
value = mempty [1, 2, 3]

-- >>> value
-- []
```

## Traverse

다음 값이 있을 때,

### 실습 16

```haskell
ids :: [(String, Int)]
ids = [("John", 1), ("Jane", 2), ("Jim", 3)]

names :: [String]
names = ["John", "Jane", "Jim"]
```

다음 함수를 만들어라

```haskell
ages :: [Maybe Int]
```

### 실습 17

그리고 다음 함수에 만들어보시오.

```haskell
ages' :: Maybe [Int]
```