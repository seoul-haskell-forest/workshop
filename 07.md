# 하스켈 배움터 7일

## 준비물

프로젝트에 다음 의존성을 `package.yaml`에 추가하고 `stack build` 하기

```yaml
dependencies:
  ...
  - relude
  - aeson
  - wai
  - http-types
  - unliftio
  - scotty
```

## 코드 정리

### relude 적용하기 

* https://hackage.haskell.org/package/relude
* `package.yaml` 언어 확장에 다음 추가
  ```yaml
  default-extensions:
  ...
  - NoImplicitPrelude
  ```
* 모든 파일에 `import Relude` 추가
  * `show`는 이제 `String`을 리턴하지 않는다.
  * `read`는 안전하지 않기 때문에 `readMaybe`를 쓴다.
  * 자주 쓰는 모듈은 reexport 되어 있기 때문에 하지 않아도 된다.

* 문자열 변환
  * https://hackage.haskell.org/package/relude-1.2.2.0/docs/Relude-String.html

### 데이터베이스 코드 옮기기

* `Infra.PgDatabase.Connection` 모듈로 데이터베이스 연결 해제 코드 옮김

```haskell
module Infra.PgDatabase.Connection where

import Control.Exception (bracket)
import Database.PostgreSQL.Simple
import Relude

withConnection :: (Connection -> IO ()) -> IO ()
withConnection action = do
  bracket
    ( connect $
        ConnectInfo
          { connectHost = "localhost",
            connectPort = 5432,
            connectDatabase = "todoapp",
            connectUser = "constacts",
            connectPassword = ""
          }
    )
    close
    action
```

* `Infra.PgDatabase.Task` 모듈에 `Task` 데이터베이스 관련 코드 옮기기

```haskell
module Infra.PgDatabase.Task where

import Data.ULID (ULID)
import Database.PostgreSQL.Simple (Connection, FromRow, ToRow, execute)
import Database.PostgreSQL.Simple.FromField (FromField (fromField))
import Database.PostgreSQL.Simple.ToField (ToField (toField))
import Domain.Todo.Task (Task)
import Relude

instance ToRow Task

instance ToField ULID where
  toField ulid = toField (show ulid :: Text)

instance FromRow Task

instance FromField ULID where
  fromField f mbs = fromMaybe (error "ULID parse error") . readMaybe <$> fromField f mbs

insertTask :: Connection -> Task -> IO Bool
insertTask conn task = do
  result <- execute conn "INSERT INTO tasks (id, content, completed, craeted_at, updated_at) VALUES (?, ?, ?, ?, ?)" task
  pure $ result == 1
```

## JSON

* JSON 라이브러리 `aeson`
  * https://hackage.haskell.org/package/aeson

### 하스켈 타입 -> JSON

* ToJSON 타입 클래스
* Generic으로 자동 구현

```haskell
{-# LANGUAGE DeriveGeneric #-}

import GHC.Generics
import Data.Aeson (ToJSON (..), encode)

data Person = Person {
      name :: Text
    , age  :: Int
    } deriving (Generic, Show)

instance ToJSON Person where
  toEncoding = genericToEncoding defaultOptions

-- >>> encode (Person {name = "Joe", age = 12})
-- "{\"name\":\"Joe\",\"age\":12}"
```

* 직접 `toJSON` 구현하기

```haskell
data Person = Person
  { name :: Text,
    age :: Int,
    type' :: Int
  }
  deriving (Show)

instance ToJSON Person where
  toJSON (Person name age type') =
    object
      [ "name" .= name,
        "age" .= age,
        "type" .= type'
      ]
```

```haskell
data PersonType = User | Admin deriving (Show)

instance ToJSON PersonType where
  toJSON User = String "user"
  toJSON Admin = String "admin"

data Person = Person
  { name :: Text,
    age :: Int,
    type' :: PersonType
  }
  deriving (Show)

instance ToJSON Person where
  toJSON (Person name age type') =
    object
      [ "name" .= name,
        "age" .= age,
        "type" .= type'
      ]
```

### JSON -> 하스켈 타입

* FromJSON 타입 클래스
* Generic으로 자동 구현

```haskell
{-# LANGUAGE OverloadedStrings #-}

import Data.Aeson (ToJSON (..), encode, FromJSON (..), decode)

...

data Person = Person {
      name :: Text
    , age  :: Int
    } deriving (Generic, Show)

instance FromJSON Person

-- >>> decode "{\"name\":\"Joe\",\"age\":12}" :: Maybe Person
-- Just (Person {name = "Joe", age = 12})
```

* 직접 `parseJSON` 구현하기

```haskell
data Person = Person
  { name :: Text,
    age :: Int,
    type' :: Int
  }
  deriving (Show)

instance FromJSON Person where
  parseJSON = withObject "Person" $ \v ->
    Person
      <$> v .: "name"
      <*> v .: "age"
      <*> v .: "type"
```

```haskell
data PersonType = User | Admin deriving (Show)

instance FromJSON PersonType where
  parseJSON (String "user") = pure User
  parseJSON (String "admin") = pure Admin
  parseJSON _ = fail "Invalid person type"

data Person = Person
  { name :: Text,
    age :: Int,
    type' :: PersonType
  }
  deriving (Show)

-- >>> decode "{\"name\":\"Joe\",\"age\":12,\"type\":\"admin\"}" :: Maybe Person
-- Just (Person {name = "Joe", age = 12, type = Admin})

-- >>> decode "{\"name\":\"Joe\",\"age\":12,\"type\":\"unknown\"}" :: Maybe Person
-- Nothing

-- >>> eitherDecode "{\"age\":12,\"name\":\"Joe\",\"type\":\"unknown\"}" :: Either String Person
-- Left "Error in $.type: Invalid person type"
```

### 실습

* Task 타입에 대해 ToJSON 인스턴스를 만들어보라. `ULID`는 `JSON`으로 바꿀 때 `show`를 이용해 문자열로 처리

```haskell
data Task = Task
  { id :: ULID,
    content :: Text,
    completed :: Bool,
    createdAt :: UTCTime,
    updatedAt :: Maybe UTCTime
  }
  deriving (Show)
```

## 웹

* scotty 패키지로 간단한 API 인터페이스를 만들어보자.
 * https://hackage.haskell.org/package/scotty-0.22/docs/Web-Scotty-Trans.html
* scotty에는 IO 모나드에 쓸 수 있는 `Web.Scotty`과 모나드 트랜스포머에 쓸 수 있는 `Web.Scotty.Trans` 모듈이 있다.
* 대부분 애플리케이션을 모나드 트랜스포머를 쓰기 때문에 `Web.Scotty.Trans` 모듈을 사용하자.

### 간단한 예제

```haskell
main :: IO ()
main = scottyT 3000 id $ do
  get "/" $ do
    html "<h1>Hello, World!</h1>"
```

* `scottyT :: (Monad m, MonadIO n) => Port -> (m Response -> IO Response) -> ScottyT m () -> n ()`
  * Port는 숫자
  * `m Response -> IO Response` 함수는 runner인데, IO가 바닥인 모나드 스택 `m`을 벗겨네는 작업이다.
    `runReaderT`, `runWriterT` 등등 해서 결국 바닥인 `IO`가 나오면 된다.
  * `ScottyT m ()` 모나드
    * 여기서 라우팅을 처리한다.
    * `get`, `post`, `put`, `delete`등과 같은 라우팅 함수가 있다.
  
* `get :: MonadUnliftIO m => RoutePattern -> ActionT m () -> ScottyT m ()`
  * RoutePattern은 문자열
  * `ActionT m ()` 모나드에서 요청에 대한 처리를 한다.
    * 응답 내용, 상태코드, 헤더 등을 설정할 수 있다.

### 응답 

```haskell
import Network.HTTP.Types (status201) -- http-types 패키지

main :: IO ()
main = scottyT 3000 id $ do
  get "/" $ do
    status status201
    setHeader "Content-Type" "plain/text"
    raw "<h1>Hello, World!</h1>"
```

```haskell
main :: IO ()
main = scottyT 3000 id $ do
  get "/" $ do
    json True
```

### 요청

* 요청 경로 `pathParam :: (Parsable a, MonadIO m) => Text -> ActionT m a`

```haskell
main :: IO ()
main = scottyT 3000 id $ do
  get "/foo/:baz" $ do
    baz <- pathParam "baz"
    text $ "파라미터: " <> baz
```

* 요청 바디 `body :: MonadIO m => ActionT m ByteString`

```haskell
main :: IO ()
main = scottyT 3000 id $ do
  post "/foo" $ do
    reqBody <- body
    text $ "파라미터: " <> decodeUtf8 reqBody
```

```sh
curl -X POST http://localhost:3000/foo -d "hello"
```

* JSON body `jsonData :: (FromJSON a, MonadIO m) => ActionT m a`
* 요청 쿼리 `queryParam :: (Parsable a, MonadIO m) => Text -> ActionT m a`
* 요청 헤더 `header :: Monad m => Text -> ActionT m (Maybe Text)`

### 실습

* `App a` 타입을 `m Response -> IO Response`로 바꿀 수 있는 러너 함수인 
  `runner :: App Response -> IO Response`를 구현하라.
* `newtype App a = App {unApp :: ReaderT Connection IO a}` 

### 실습

* `POST /task`를 불렀을 때 `ActionT m ()`에서 `TaskUseCase.createTask "Hello"`를 부르고 결과에 따라 성공했다면
  만들어진 Task를 JSON으로 응답하고 실패했다면 400 응답 코드에 JSON 응답으로 `{"error": "메시지"}`를 돌려줘라

### 실습

* `POST /task`를 부를 때 `{"content": "내용"}` JSON 형태를 요청 body에 보내고 `TaskUseCase.createTask`를 
  부를 때 요청 바디에 있는 content를 꺼내 넘겨줘라.